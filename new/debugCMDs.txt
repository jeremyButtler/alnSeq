set confirm off
set confirm off
#break alnSeq.c:400
break needleman.c:384
#break seqStruct.c:522
#break alnStruct.c:841
#break hirschberg.c:112

define runcmd
  run -use-hirschberg -query ../analysis/genomes/Small-query.fasta -ref ../analysis/genomes/Large-reference.fasta
  #run -use-needle -line-wrap 0 -query ../analysis/genomes/Mid-query.fasta -ref ../analysis/genomes/Mid-reference.fasta
  #run -use-water -line-wrap 0 -query ukrCrimeaEnv.fasta -ref ukrCrimeaEnv.fasta
  #run -query test-qry.fasta -ref test-ref.fasta
end

runcmd

define pscores
   printf "ret: %i\nins: %i", *retScL, *insScL
   printf "\nsnp: %i\ndel: %i", *snpScL, *delScL
   printf "\ndir: %i\n(1=del, 2=ins, 3=snp)\n", dirUC
end

# Print number of gaps/snps/matches in alignment
define sumaln
   set $alnStr = $arg0
   set $noBaseI = 0
   set $softMaskI = 0
   set $gapsI = 0
   set $snpsI = 0
   set $matchesI = 0

   # Get of leading zeros
   while(*$alnStr == 0)
      set $alnStr = $alnStr + 1
   end

   # Count till end of alignment
   while(*$alnStr != 0)
      if *$alnStr == 0
         set $noBaseI = $noBaseI + 1
      end

      if *$alnStr == 1
         set $softMaskI = $softMaskI + 1
      end

      if *$alnStr == 2
         set $gapsI = $gapsI + 1
      end

      if *$alnStr == 3
         set $snpsI = $snpsI + 1
      end

      if *$alnStr == 4
         set $matchesI = $matchesI + 1
      end

      set $alnStr = $alnStr + 1
   end

   printf "noBase:  %i\n", $noBaseI
   printf "sMasks:  %i\n", $softMaskI
   printf "gaps:    %i\n", $gapsI
   printf "snps:    %i\n", $snpsI
   printf "matches: %i\n", $matchesI

   set $totalI = $noBaseI + $softMaskI + $gapsI
   set $totalI = $totalI + $snpsI + $matchesI

   printf "total:   %i\n", $totalI
end
     

# Print out the hirschberg reverse and forward score rows
define phirschscores
  printf "%3li", forwardIndelColL
  set $iIndex = 0

  while($iIndex < refLenUL)
    printf " %3li", *(forwardScoreRowL +refStartUL+$iIndex)
    set $iIndex = $iIndex + 1
  end

  printf "\n"
  set $iIndex = 0
 
  while($iIndex < refLenUL)
    printf "%3li ", *(reverseScoreRowL +refStartUL+$iIndex)
    set $iIndex = $iIndex + 1
  end

  printf "%3li\n", reverseIndelColL

  set $iIndex = 0
  set $sumI =  *(reverseScoreRowL + refStartUL)
  set $sumI = $sumI + forwardIndelColL

  printf "%3li ", $sumI

  while($iIndex < refLenUL - 1)
    set $sumI = *(forwardScoreRowL + refStartUL +$iIndex)
    set $iIndex = $iIndex + 1

    set $sumI=$sumI+*(reverseScoreRowL+refStartUL+$iIndex)
    printf "%3li ", $sumI
  end

  set $sumI =  *(forwardScoreRowL + refLenUL - 1)
  set $sumI = $sumI + reverseIndelColL

  printf "%3li\n", $sumI
end

# Quick function to get length of a string
define lenstr
  set $tmpStr = $arg0
  set $lenI = 0

  while(*$tmpStr != '\0')
    set $tmpStr = $tmpStr + 1
    set $lenI = $lenI + 1
  end

  p $lenI
end

#break hirschberg.c:464 if midPointUI - 1 + refOffsetUI < refOffsetUI
